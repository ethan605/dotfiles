# vim:fileencoding=utf-8:filetype=zsh:foldenable:foldmethod=marker:foldmarker=[[[,]]]
# [[[ Global variables
export LANG=en_GB.UTF-8
export LC_COLLATE="$LANG"
export LC_CTYPE="$LANG"
export LC_MESSAGES="$LANG"
export LC_MONETARY="$LANG"
export LC_NUMERIC="$LANG"
export LC_TIME="$LANG"
export LC_ALL="$LANG"

export CC=clang
export CXX=clang++
export TOOLCHAINS=swift

export EDITOR=nvim
export REACT_EDITOR=nvim
export VISUAL=nvim

export BAT_THEME='Sublime Snazzy'
export LESS='--LONG-PROMPT --RAW-CONTROL-CHARS --squeeze-blank-lines --tabs=2'
export MANPAGER='less +Gg'

export COMPOSE_BAKE=true
export DOCKER_HOST="unix://$HOME/.orbstack/run/docker.sock"
export NODE_OPTIONS="--max-old-space-size=8192"

export FZF_DEFAULT_OPTS='
  --bind=ctrl-y:preview-up,ctrl-e:preview-down,ctrl-u:preview-page-up,ctrl-d:preview-page-down
  --color=fg+:7,bg+:0,hl:5,hl+:5
  --color=gutter:0,header:4,marker:2,pointer:5,prompt:2,spinner:3
  --cycle
  --marker="›"
  --pointer="›"
  --prompt="❯ "
  --reverse
  --no-height
'
export FZF_CTRL_R_OPTS='
  --preview="echo {2..} | bat --language=zsh --color=always"
  --bind="ctrl-y:execute-silent(echo -n {2..} | pbcopy)+abort"
  --header="Press CTRL-Y to copy command into clipboard"
'

# XDG base directories
export XDG_CONFIG_HOME="$HOME/.config"
export XDG_CACHE_HOME="$HOME/.cache"
export XDG_DATA_HOME="$HOME/.local/share"

{{- if eq .chezmoi.os "linux" }}

export XDG_DATA_DIRS=/usr/local/share:/usr/share:/var/lib/flatpak/exports/share
export XDG_CONFIG_DIRS=/etc/xdg

export TERMINFO=/usr/share/terminfo
export GPG_TTY=$(tty)

# Input method
export GTK_IM_MODULE=fcitx
export XMODIFIERS=@im=fcitx
export QT_IM_MODULE=fcitx

{{- else if eq .chezmoi.os "darwin" }}

# Rewrite $PATH
PATH="/usr/local/bin:/usr/local/sbin:$(getconf PATH)"

# Homebrew
export PATH="/opt/homebrew/bin:/opt/homebrew/sbin:$PATH"

# Replace macOS utilities with GNU ones
PATH="$(brew --prefix coreutils)/libexec/gnubin:$PATH"
PATH="$(brew --prefix diffutils)/bin:$PATH"
PATH="$(brew --prefix findutils)/libexec/gnubin:$PATH"
PATH="$(brew --prefix gnu-getopt)/bin:$PATH"
PATH="$(brew --prefix gnu-indent)/libexec/gnubin:$PATH"
PATH="$(brew --prefix gnu-sed)/libexec/gnubin:$PATH"
PATH="$(brew --prefix gnu-tar)/libexec/gnubin:$PATH"
PATH="$(brew --prefix grep)/libexec/gnubin:$PATH"
PATH="$(brew --prefix llvm)/bin:$PATH"

{{- end }}

PATH="$HOME/.local/bin:$PATH"
export PATH
#]]]

# [[[ Init functions
function __load-db-clickhouse() {
  export SQL_TARGET=clickhouse

  local port=9440
  local suffix=":$port/encord_analytics"
  local uri_local="clickhouse://default:default@localhost${suffix/9440/9000}" uri_dev uri_stg uri_eu_ro uri_us_ro

  uri_dev="clickhouse://$(__op-clickhouse-dev-get username):$(__op-clickhouse-dev-get password --reveal)@$(__op-clickhouse-dev-get 'server url')$suffix"
  uri_stg="clickhouse://$(__op-clickhouse-stg-get username):$(__op-clickhouse-stg-get password --reveal)@$(__op-clickhouse-stg-get 'server url')$suffix"
  uri_eu_ro="clickhouse://$(__op-clickhouse-eu-ro-get username):$(__op-clickhouse-eu-ro-get password --reveal)@$(__op-clickhouse-eu-ro-get 'server url')$suffix"
  uri_us_ro="clickhouse://$(__op-clickhouse-us-ro-get username):$(__op-clickhouse-us-ro-get password --reveal)@$(__op-clickhouse-us-ro-get 'server url')$suffix"

  # For nvim-dbee
  export DBEE_CONNECTIONS='[
    { "type": "clickhouse", "name": "5-clickhouse-local", "url": "'$uri_local'" },
    { "type": "clickhouse", "name": "4-clickhouse-dev", "url": "'$uri_dev'?secure=true" },
    { "type": "clickhouse", "name": "3-clickhouse-stg", "url": "'$uri_stg'?secure=true" },
    { "type": "clickhouse", "name": "2-clickhouse-prd-us-ro", "url": "'$uri_us_ro'?secure=true" },
    { "type": "clickhouse", "name": "1-clickhouse-prd-eu-ro", "url": "'$uri_eu_ro'?secure=true" }
  ]'
}

function __load-db-postgres() {
  export SQL_TARGET=postgres

  local suffix=":5432/postgres"
  local uri_local="postgres://postgres:postgres@localhost$suffix" uri_dev uri_stg uri_prd_eu_ro uri_prd_us_ro

  uri_dev="postgres://$(__op-dev-get username):$(__op-dev-get password --reveal)@$(__op-dev-get 'DEV IP')$suffix"
  uri_stg="postgres://$(__op-dev-get username):$(__op-dev-get password --reveal)@$(__op-dev-get 'STAGING IP')$suffix"
  uri_prd_eu_ro="postgres://$(__op-prd-eu-ro-get username):$(__op-prd-eu-ro-get password --reveal)@$(__op-prd-eu-ro-get ip)$suffix"
  uri_prd_us_ro="postgres://$(__op-prd-us-ro-get username):$(__op-prd-us-ro-get password --reveal)@$(__op-prd-us-ro-get server)$suffix"

  # Escaping all '%' characters
  uri_prd_eu_ro=${uri_prd_eu_ro//\%/\%25}

  export DBEE_CONNECTIONS='[
    { "type": "postgres", "name": "5-postgres-local", "url": "'$uri_local'?sslmode=disable" },
    { "type": "postgres", "name": "4-postgres-dev", "url": "'$uri_dev'?sslmode=require" },
    { "type": "postgres", "name": "3-postgres-stg", "url": "'$uri_stg'?sslmode=require" },
    { "type": "postgres", "name": "2-postgres-prd-us-ro", "url": "'$uri_prd_us_ro'?sslmode=require" },
    { "type": "postgres", "name": "1-postgres-prd-eu-ro", "url": "'$uri_prd_eu_ro'?sslmode=require" }
  ]'
}

function __load-key-bindings() {
  # Set editor default keymap to vi
  bindkey -v

  autoload -U edit-command-line
  zle -N edit-command-line
  bindkey -M vicmd 'vv' edit-command-line

  # Word manipulation
  bindkey '^w' backward-kill-word

  # Vim-like bindings
  bindkey '^h' backward-word        # left
  bindkey '^l' forward-word         # right
  bindkey '^k' up-line-or-search    # up
  bindkey '^j' down-line-or-search  # down

  # Ctrl-a and Ctrl-e to move to beginning/end of line
  bindkey '^a' beginning-of-line
  bindkey '^e' end-of-line

  # Shift+Tab as reverse completion
  bindkey '^[[Z' reverse-menu-complete

  # Bind up and down keys
  zmodload -F zsh/terminfo +p:terminfo
  if [[ -n ${terminfo[kcuu1]} && -n ${terminfo[kcud1]} ]]; then
    bindkey "${terminfo[kcuu1]}" history-substring-search-up
    bindkey "${terminfo[kcud1]}" history-substring-search-down
  fi

  bindkey -M vicmd 'k' history-substring-search-up
  bindkey -M vicmd 'j' history-substring-search-down
}

function __load-mise() {
  eval "$(mise activate zsh)"

  # mise-dependent components
  # shellcheck disable=SC1091
  source "$(mise where gcloud)/completion.zsh.inc"
}

function __load-ssh() {
{{- if eq .chezmoi.os "darwin" }}

  defaults write org.gpgtools.common UseKeychain NO

{{- end }}

  unset SSH_AGENT_PID

  if [[ "${gnupg_SSH_AUTH_SOCK_by:-0}" -ne $$ ]]; then
    SSH_AUTH_SOCK="$(gpgconf --list-dirs agent-ssh-socket)"
    export SSH_AUTH_SOCK
  fi

  GPG_TTY=$(tty)
  export GPG_TTY

  gpg-connect-agent updatestartuptty /bye >/dev/null
}

{{- if eq .chezmoi.os "linux" }}

function __load-sway() {
  if [[ -z $DISPLAY ]] && [[ $(tty) = /dev/tty1 ]]; then
    export _JAVA_AWT_WM_NONREPARENTING=1
    # export QT_QPA_PLATFORM=wayland-egl
    # export QT_QPA_PLATFORMTHEME=qt5ct
    # export QT_WAYLAND_FORCE_DPI=physical
    # export QT_WAYLAND_DISABLE_WINDOWDECORATION=1
    export QT_QPA_PLATFORM=wayland
    export SDL_VIDEODRIVER=wayland
    export XDG_CURRENT_DESKTOP=sway
    export XDG_SESSION_TYPE=wayland
    export TERM=foot
    export MOZ_ENABLE_WAYLAND=1

{{- if eq .chezmoi.hostname "PF3XSEPR" }}
    export BROWSER=google-chrome-stable
{{- else }}
    export BROWSER=firefox
{{- end }}

    exec sway -d 2> $HOME/.logs/sway.log
  fi
}

{{- end }}

function __load-work-envs() {
  export WORK_DIR="$HOME/work"

  if [[ -d "$WORK_DIR/.env/bin" ]]; then
    export PATH="$WORK_DIR/.env/bin:$PATH"
  fi

  if [[ -f "$WORK_DIR/.env/.zshrc" ]]; then
    # shellcheck disable=SC1091
    source "$WORK_DIR/.env/.zshrc"
  fi
}

function __load-zim() {
  if [[ ! "${ZIM_HOME}/init.zsh" -nt "${ZDOTDIR:-${HOME}}/.zimrc" ]]; then
    # Update static initialization script if it does not exist or it's outdated, before sourcing it
    # shellcheck disable=SC1091
    source "${ZIM_HOME}/zimfw.zsh" init -q
  fi

  # shellcheck disable=SC1091
  source "${ZIM_HOME}/init.zsh"

  __load-zsh-plugins
  __load-key-bindings
  __load-zsh-settings
  __load-fzf-tab
}

function __load-zsh-plugins() {
  eval "$(direnv hook zsh)"
  eval "$(helm completion zsh)"
  eval "$(starship init zsh)"
  eval "$(zoxide init zsh)"
}

function __load-zsh-settings() {
  # History settings
  HISTFILE="$HOME/.zhistory"
  HISTSIZE=1000000                 # 1 million entries
  # shellcheck disable=SC2034
  SAVEHIST=1000000                 # 1 million entries

  setopt BANG_HIST                 # Treat the '!' character specially during expansion.
  setopt EXTENDED_HISTORY          # Write the history file in the ":start:elapsed;command" format.
  setopt INC_APPEND_HISTORY        # Write to the history file immediately, not when the shell exits.
  setopt SHARE_HISTORY             # Share history between all sessions.
  setopt HIST_EXPIRE_DUPS_FIRST    # Expire duplicate entries first when trimming history.
  setopt HIST_IGNORE_DUPS          # Don't record an entry that was just recorded again.
  setopt HIST_IGNORE_ALL_DUPS      # Delete old recorded entry if new entry is a duplicate.
  setopt HIST_FIND_NO_DUPS         # Do not display a line previously found.
  setopt HIST_IGNORE_SPACE         # Don't record an entry starting with a space.
  setopt HIST_SAVE_NO_DUPS         # Don't write duplicate entries in the history file.
  setopt HIST_REDUCE_BLANKS        # Remove superfluous blanks before recording entry.
  setopt HIST_VERIFY               # Don't execute immediately upon history expansion.
  setopt HIST_BEEP                 # Beep when accessing nonexistent history.

  # Customize spelling correction prompt.
  # shellcheck disable=SC2034
  SPROMPT='zsh: correct %F{red}%R%f to %F{green}%r%f [nyae]? '

  # Remove usable components (/.-=) from WORDCHARS.
  WORDCHARS=${WORDCHARS//[\/\.\-=]}

  # Set what highlighters will be used.
  # See https://github.com/zsh-users/zsh-syntax-highlighting/blob/master/docs/highlighters.md
  # shellcheck disable=SC2034
  ZSH_HIGHLIGHT_HIGHLIGHTERS=(main brackets)

  # Prompt for spelling correction of commands.
  setopt CORRECT

  # Set a custom path for the completion dump file.
  # If none is provided, the default ${ZDOTDIR:-${HOME}}/.zcompdump is used.
  zstyle ':zim:completion' dumpfile "${ZDOTDIR:-${HOME}}/.zcompdump-${ZSH_VERSION}"

{{- if eq .chezmoi.os "linux" }}

  autoload -Uz add-zsh-hook
  add-zsh-hook -Uz chpwd osc7_cwd

{{- end }}
}

function __load-fzf-tab() {
  zstyle -d ':completion:*' format
  zstyle ':completion:*:git-checkout:*' sort false
  zstyle ':completion:*:descriptions' format '[%d]'
  # shellcheck disable=SC2086,SC2296
  zstyle ':completion:*' list-colors ${(s.:.)LS_COLORS}
  zstyle ':completion:*' menu no

  FZF_TAB_GROUP_COLORS=(
      $'\033[32m' $'\033[33m' $'\033[36m' $'\033[37m' $'\033[31m' $'\033[35m'
  )
  # shellcheck disable=SC2086,SC2128
  zstyle ':fzf-tab:*' group-colors $FZF_TAB_GROUP_COLORS

  zstyle ':fzf-tab:*' prefix ''
  zstyle ':fzf-tab:*' switch-group '[' ']'
  zstyle ':fzf-tab:*' use-fzf-default-opts yes
}
#]]]

# [[[ Utils
function oc() {
  ANTHROPIC_API_KEY="$(pass work/anthropic-api-key)"
  export ANTHROPIC_API_KEY

  # For postgresql MCP
  export POSTGRES_CONNECTION_STRING="postgres://postgres:postgres@localhost:5432/postgres"

  # For LSP servers
  export PATH="$HOME/.local/share/nvim/mason/bin:$PATH"
  export OPENCODE_DISABLE_LSP_DOWNLOAD=true
  export OPENCODE_EXPERIMENTAL_LSP_TOOL=true

  opencode "$@"
}

function greek-letters() {
  print -P "alpha: %F{red}α%f,beta: %F{red}β%f,gamma: %F{red}γ%f,delta: %F{red}δ%f,epsilon: %F{red}ε%f,theta: %F{red}θ%f,lambda: %F{red}λ%f,mu: %F{red}μ%f,pi: %F{red}π%f,sigma: %F{red}σ%f,phi: %F{red}φ%f,omega: %F{red}ω%f" \
    | tr ',' '\n' \
    | fzf --ansi \
    | awk '{ print $2 }'
}

function mux() {
  local workspace=${1:-$(greek-letters)}
  tmux attach -t "$workspace" || tmux new -s "$workspace"
}

{{- if eq .chezmoi.os "linux" }}

function nord_dns() {
  device=$(/usr/bin/ls /sys/class/ieee80211/*/device/net)
  nmcli dev mod $device ipv4.dns "103.86.96.100 103.86.99.100"
}

function osc7_cwd() {
  setopt localoptions extendedglob
  input=( ${(s::)PWD} )
  uri=${(j::)input/(#b)([^A-Za-z0-9_.\!~*\'\(\)-\/])/%${(l:2::0:)$(([##16]#match))}}
  print -n "\e]7;file://${HOSTNAME}${uri}\e\\"
}

{{- end }}
#]]]

# [[[ Autoloads
{{- if eq .chezmoi.os "linux" }}
# Sway goes first
__load-sway
{{- end }}

# Work envs
__load-work-envs

# Base envs
__load-mise
__load-zim

# SSH keys
__load-ssh
#]]]

# [[[ Aliases
# Remap system commands
alias nano=nvim
alias rm='rm -i'
alias vi=nvim
alias vim=nvim

{{- if eq .chezmoi.os "linux" }}

alias clippick='clipman pick --print0 --tool=CUSTOM --tool-args="fzf --read0"'
alias load-time='time /usr/bin/zsh -ic exit'
alias ncmpcpp='ncmpcpp --bindings $HOME/.config/ncmpcpp/bindings'

{{- else if eq .chezmoi.os "darwin" }}

alias bupdate='brew update --verbose && brew upgrade --verbose && brew cleanup --prune=all && brew doctor'
alias bbundle='brew bundle dump --describe --global --force'
alias load-time='time /opt/homebrew/bin/zsh -ic exit'
alias xdg-open='open'

{{- end }}

alias color-palette='curl -s https://gist.githubusercontent.com/ethan605/ea1b698c3395b9339748e8a0131136a5/raw | bash'
alias dcompose='docker compose --profile'
alias kargo='kubecolor argo rollouts'
alias zim='zimfw upgrade && zimfw update && zimfw clean && zimfw build && zimfw compile'

# Eza
alias lk='ll --total-size --sort=size --reverse'
alias lr='ls --git --tree --level=2'

# Neovim
alias v=nvim
alias dbee='nvim +Dbee'
alias repl='nvim +Repl'
alias vsync='nvim --headless "+Lazy! sync" +MasonUpdate +qa'
alias vrestore!='nvim --headless "+Lazy! restore" +qa'

# Python
alias pre-commit-fix='pre-commit run --color=always --all-files --hook-stage=pre-push'
alias ptest='uv run pytest -n auto -W ignore::DeprecationWarning -W ignore::UserWarning --log-cli-level=INFO'
alias itest='ptest -vvn0 --pdb --pdbcls=IPython.terminal.debugger:Pdb'

# Shortcuts
alias a='axel --alternate --num-connections=8 --output $HOME/Downloads/'
alias c=chezmoi
alias f=vifm
alias k=kubecolor
alias w='watch --color --differences --errexit --exec'
# ]]]
